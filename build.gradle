plugins {
    id 'java-library'
    id 'maven'
    id 'pl.allegro.tech.build.axion-release' version '1.10.0'
    id "com.jfrog.bintray" version "1.8.5"
}

repositories {
    maven { url 'http://repo.bodar.com/' }
    mavenCentral()
}

group = 'com.github.nickmcdowall'
sourceCompatibility = '11'
project.version = scmVersion.version

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    configurations = ['archives']
    pkg {
        repo = 'nkm'
        name = 'yatspec'
        override = false
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/nickmcdowall/yatspec.git'
        publish = true
        publicDownloadNumbers = true
        version {
            name = project.version
        }
    }
}

ext {
    junitJupiter = '5.3.2'
    junitPlatformVersion = '1.3.2'
}

configurations {
    inFatJar
}

// Add non-maven-central dependencies to jar (fat jar)
jar {
    from {
        configurations.inFatJar.collect {
            zipTree(it)
        }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

dependencies {
    inFatJar "com.googlecode.funclate:funclate:110"
    implementation "com.googlecode.funclate:funclate:110"

    api "net.sourceforge.plantuml:plantuml:1.2020.9"
    api "antlr:antlr:2.7.7"
    api "org.antlr:stringtemplate:3.2.1"
    api "com.thoughtworks.qdox:qdox:1.12.1"
    api "jaxen:jaxen:1.1.1"
    api "org.jdom:jdom:1.1"
    api "org.junit.jupiter:junit-jupiter-api:$junitJupiter"
    api "org.junit.jupiter:junit-jupiter-params:$junitJupiter"
    api "org.junit.jupiter:junit-jupiter-engine:$junitJupiter"
    api "org.hamcrest:hamcrest-library:1.3"

    testCompile 'org.assertj:assertj-core:3.15.0'

    implementation "org.apache.commons:commons-lang3:3.8.1"
    implementation "org.jtwig:jtwig-core:5.87.0.RELEASE"
}

// Remove non-maven-central dependencies from generated pom (bundled in jar)
install.repositories.mavenInstaller.pom.whenConfigured { pom ->
    pom.dependencies.removeAll {
        dep -> dep.groupId ==~ /com\.googlecode.*/
    }
}

artifacts {
    archives jar
    archives sourcesJar
}

test {
    systemProperty "yatspec.output.dir", "$buildDir/reports/yatspec"
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
    }
}

wrapper {
    gradleVersion = "5.3"
}