plugins {
    id 'java-library'
    id 'maven'
    id 'pl.allegro.tech.build.axion-release' version '1.10.0'
}

repositories {
    maven { url 'http://repo.bodar.com/' }
    mavenCentral()
}

group = 'com.github.nickmcdowall'
sourceCompatibility = '11'
project.version = scmVersion.version

ext {
    junitJupiter = '5.3.2'
    junitPlatformVersion = '1.3.2'
}

configurations {
    inFatJar
}

// Add non-maven-central dependencies to jar (fat jar)
jar {
    from {
        configurations.inFatJar.collect {
            zipTree(it)
        }
    }
}

dependencies {
    inFatJar "com.googlecode.funclate:funclate:110"
    implementation "com.googlecode.funclate:funclate:110"

    api "net.sourceforge.plantuml:plantuml:1.2019.5"
    api "antlr:antlr:2.7.7"
    api "org.antlr:stringtemplate:3.2.1"
    api "com.thoughtworks.qdox:qdox:1.12.1"
    api "jaxen:jaxen:1.1.1"
    api "org.jdom:jdom:1.1"
    api "org.junit.jupiter:junit-jupiter-api:$junitJupiter"
    api "org.junit.jupiter:junit-jupiter-params:$junitJupiter"
    api "org.junit.jupiter:junit-jupiter-engine:$junitJupiter"
    api "org.hamcrest:hamcrest-library:1.3"

    testCompile 'org.assertj:assertj-core:3.12.2'

    implementation "org.apache.commons:commons-lang3:3.8.1"
    implementation "org.jtwig:jtwig-core:5.87.0.RELEASE"


}

// Remove non-maven-central dependencies from generated pom (bundled in jar)
install.repositories.mavenInstaller.pom.whenConfigured { pom ->
    pom.dependencies.removeAll {
        dep -> dep.groupId ==~ /com\.googlecode.*/
    }
}

test {
    useJUnitPlatform()
}

wrapper {
    gradleVersion = "5.3"
}